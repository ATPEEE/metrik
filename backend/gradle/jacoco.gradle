apply plugin: "jacoco"

String[] exclusionPatterns = [
        "fourkeymetrics/Application**",
        "**/applicationconfig**",
        "**/dto/**",
        "**/vo/**",
        "**/model/**",
        "**/common/validation/**",
        "**/project/controller/TestController**",
        "**GlobalExceptionHandler**"
]

project.ext.set('coverageExclusionPatterns', exclusionPatterns)

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    shouldRunAfter test
    reports {
        xml.enabled false
        csv.enabled false
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: exclusionPatterns)
        })
    }
}

jacocoTestCoverageVerification {
    shouldRunAfter test
    executionData test
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                minimum = 0.90
            }
        }

        rule {
            element = 'BUNDLE'
            limit {
                counter = 'BRANCH'
                minimum = 0.87
            }
        }
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: exclusionPatterns)
        })
    }
}

test {
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
